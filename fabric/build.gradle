plugins {
    id 'fabric-loom' version "${loom_version}" apply false
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'fabric-loom'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = "${mod_version}"
    group = "${mod_group_id}.fabric"

    base {
        archivesName = "${mod_id}-${loader_name}-${minecraft_version}"
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencies {

        if (minecraft_version == 'universal') {
            minecraft "com.mojang:minecraft:${subprojects.last().minecraft_version}" // reserved for fabric loom
        } else {
            minecraft "com.mojang:minecraft:${minecraft_version}"
        }

        mappings loom.officialMojangMappings() // reserved for fabric loom

        modImplementation "net.fabricmc:fabric-loader:${loader_version}"

        implementation project(':common')
        implementation project(':client')

        shadow project(':common')
        shadow project(':client')

        implementation 'com.google.code.findbugs:jsr305:3.0.2'
    }

    processResources {

        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        filesMatching('fabric.mod.json') {
            expand "mod_version": mod_version,
                    "mod_id": "${mod_id}-port-${minecraft_version.replace(".", "-")}",
                    "mod_name": mod_name,
                    "mod_description": mod_description,
                    "minecraft_version": minecraft_version
        }

        from project(':common').sourceSets.main.resources
        from project(':client').sourceSets.main.resources
    }

    shadowJar {
        configurations = [project.configurations.shadow]
        dependencies {
            exclude dependency('com.google.code.findbugs:jsr305')
        }
    }

    remapJar {
        dependsOn shadowJar
        inputFile = shadowJar.archiveFile
    }

}

subprojects {

    dependencies {
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

        implementation project(":vanilla:$project.name")
        shadow project(":vanilla:$project.name")
    }

    processResources {
        from project(":vanilla:${project.name}").file("src/main/resources")
    }

    loom {
        accessWidenerPath = project(":vanilla:$project.name").file("src/main/resources/effortless.accesswidener")
        mixin {
            add(sourceSets.main, 'effortless.fabric.refmap.json')
        }
    }

    shadowJar {
        dependencies {
            include project(":vanilla:${project.name}")
        }
        relocate 'dev.huskuraft.effortless.vanilla', 'dev.huskuraft.effortless.fabric'
    }

}

remapJar {
    afterEvaluate {
        subprojects.each {
            nestedJars.from project("${it.path}").tasks.named("remapJar")
        }
    }
}

task outputJar(type: Copy) {
    from('build/libs')
    into rootProject.file("output")
    include remapJar.archiveFile.get().getAsFile().getName()
}

assemble {
    dependsOn remapJar
    finalizedBy outputJar
}

apply plugin: 'com.modrinth.minotaur'
apply from: rootProject.file('gradle/publishing/modrinth.gradle')

